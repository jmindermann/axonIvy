REM ================================================================================================================ 
REM == This batch file writes files with information about the garbage collection and
REM == memory consumption of a java process with the given main java class (ClassName)
REM == to the given output directory (OutputDir).
REM == 
REM == The following files are written each time this *.bat file is executed
REM ==
REM == Jps_YYYY_MM_DD_HH_mm.txt:
REM == Contains a list of all java processes currently visible
REM == For more information see: jps
REM == 
REM == HistoDump_PID_YYYY_MM_DD_HH_mm.txt:
REM == Contains a list of all classes with the number of instances currently living in the process with the pid 
REM == For more information see: jmap -histo:live
REM == 
REM == HeapDump_PID_YYYY_MM_DD_HH_mm.bin:
REM == Heap dump of the process with the pid 
REM == For more information see: jmap -dump:live,format=b,file=...
REM == This file is not generated by default you have to remove comments in the section -- Write heap dump files
REM == 
REM == GcStat_PID_YYYY_MM_DD_HH_mm.txt:
REM == Contains the current values for the Eden, Survivor, Old and Permanent space memory and their utilitization.
REM == Also contains the number of young and old garbage collections and their consumption time.
REM == For more information see: jstat -gc
REM ==
REM == Precondition:
REM ==
REM == 1. The bat file must be executed with the same user the java process is running (See Windows Service)
REM == 2. The java process must be executed with JRE >= 1.6
REM == 3. The JDK to use must be >= 1.6 (Same architecture x64/x86 as the JRE in 2).
REM == 4. The JvmLauncher.dll that is used to start the Axon.ivy Engine java processes must be from 
REM	==	  Axon.ivy Version >= 4.2 (Otherwise the main java class cannot be found). This is no problem if you
REM ==    start the java process with java.exe
REM ==
REM == Windows Service:
REM ==
REM == If the windows service to monitor is running under Local System Account. This *.bat file must also be 
REM == executed under this account. This can be done by configuring a scheduled task that execute this *.bat file.
REM == In the task definition the account in which the task is executed can be configured to Local System Account.
REM ==
REM == Scheduled Tasks:
REM ==
REM == Use the windows task scheduler to configure a task that executes this *.bat file periodically. This allows 
REM == to build a history of the memory consumption which can be used to analyze memory leaks.
REM ===============================================================================================================

REM -- Output directory where the dump files are written to (users may change this setting)
SET OutputDir=C:\temp\IvyMemoryStatistic

REM -- Name of the main class of the java process for which the dump files should be written for (users may change this setting)
SET ClassName="ch.ivyteam.ivy.server.WindowsService"
REM SET ClassName="ch.ivyteam.ivy.server.ServerLauncher"

REM -- The path bin directory of the jdk to use (>= 1.6) (users may change this setting)
SET JDK=c:\Program Files\Java\jdk1.6.0_18\bin

SET h=%time:~0,2%
SET mi=%time:~3,2%
SET d=%date:~0,2%
SET mo=%date:~3,2%
SET y=%date:~6,4%
SET ts=%y%_%mo%_%d%_%h%_%mi%
SET JpsOutputFile="%OutputDir%\Jps_%ts%.txt"

REM -- Write Jps File
"%JDK%\jps" -l >> %JpsOutputFile%

SET PID=
REM -- Evaluate PID of java process running the given main class
FOR /F "skip=2 tokens=1 delims= " %%G IN ('FIND %ClassName% %JpsOutputFile%') DO SET PID=%%G

IF "%PID%"=="" GOTO :ProcessNotFound

SET DumpOutputFile="%OutputDir%\HistoDump_%PID%_%ts%.txt"
SET GcStatOutputFile="%OutputDir%\GcStat_%PID%_%ts%.txt"

REM -- Write the GcStat file
"%JDK%\jstat" -gc %PID% >> %GcStatOutputFile%
REM -- Write the HistoDump file
"%JDK%\jmap" -histo:live %PID% >> %DumpOutputFile%

REM -- Write Heap Dump file
REM -- Remove the comment (REM) from the next two lines to generate heap dumps
REM SET HeapDumpOutputFile="%OutputDir%\HeapDump_%PID%_%ts%.txt"
REM "%JDK%\jmap" -dump:live,format=b,file=%HeapDumpOutputFile% %PID%

GOTO :eof
:ProcessNotFound
ECHO "Java process with main class %ClassName% not running"